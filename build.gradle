buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

def versionMajor = 1
def versionMinor = 2
def versionPatch = 1
def versionBuild = 0

apply plugin: 'com.android.application'
// TODO: This would be so nice but it doesn't work
//if (project.hasProperty('apiKey') && project.hasProperty('apiSecret')) {
// FYI: Comment the following line to prevent crashlytics errors from building the project
    apply plugin: 'io.fabric'
//}

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile 'com.android.support:support-v4:21.0.+'
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.android.support:cardview-v7:21.0.+'
    compile 'com.android.support:recyclerview-v7:21.0.+'
    compile 'com.android.support:gridlayout-v7:21.0.+'
    compile 'com.nineoldandroids:library:2.4.0'

    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'com.jakewharton.timber:timber:2.5.0'
    compile  "org.parceler:parceler-api:0.2.15"
    provided "org.parceler:parceler:0.2.15"

    compile 'com.koushikdutta.ion:ion:2.0.5'

    // TODO: Update to 1.2.4 once it's out (https://github.com/chrisbanes/PhotoView/issues/72)
    compile 'com.github.chrisbanes.photoview:library:1.2.2'

    compile('com.crashlytics.sdk.android:crashlytics:2.2.0@aar') {
        transitive = true;
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "anabolicandroids.chanobol"
        minSdkVersion 9
        targetSdkVersion 21

        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    // http://stackoverflow.com/a/21020469/283607
    signingConfigs {
        // FYI: Comment the following lines and the one under
        // buildTypes.release to prevent Gradle from aborting the build
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        release_sfw {
            initWith buildTypes.release
            applicationIdSuffix '.sfw'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

